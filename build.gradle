plugins {
    id 'fabric-loom' version '1.6-SNAPSHOT'
    id 'io.github.ladysnake.chenille' version '0.13.1'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

chenille {
    customLicense = rootProject.file("LICENSE")
    github = new URL("https://github.com/MoriyaShiine/extra-origins")
    javaVersion = 17
    configurePublishing {
        withLadysnakeMaven()
        withGithubRelease()
        withModrinthRelease()
        withCurseforgeRelease()
    }
}

modrinth {
    dependencies {
        required.project "fabric-api"
        required.project "origins"
        required.project "pehkui"
    }
    syncBodyFrom = rootProject.file("README.md").text
}

repositories {
    chenille.repositories.allCommonRepositories()
    maven {
        name "carpet"
        url "https://masa.dy.fi/maven"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings "net.fabricmc:yarn:${yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    modImplementation "com.github.apace100:origins-fabric:${origins_version}"
    modImplementation("com.github.virtuoel:pehkui:${pehkui_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}" }
    }
}